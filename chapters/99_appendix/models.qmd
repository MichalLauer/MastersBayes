## ARCH(p)

\begin{code}{Stan}{General ARCH(p) model}{code-arch}
data {
  int<lower=1> T; // Length of the TS
  int<lower=1> p; // Order of ARCH(p)
  vector[T] y;    // Log of returns
}

parameters {
  real<lower=0> alpha0; // Intercept
  simplex[p + 1] alpha; // Coefficients with stationarity
  real<lower=1> nu;     // Degrees of freedom
}

transformed parameters {
  vector[T - p] sigma2 = rep_vector(alpha0, T - p); // Conditional variance

  // Skip first *p* observations as there are no lags for them
  for (i in 1:(T - p)) {
    for (j in 1:p) {
      sigma2[i] += alpha[j] * square(y[i + p - j]);
    }
  }
}

model {
  nu     ~ gamma(4, 0.4);
  alpha0 ~ gamma(2, 4);
  alpha  ~ beta(2, 2);

  for (i in 1:(T - p)) {
    y[i + p] ~ student_t(nu, 0, sqrt(sigma2[i]));
  }
}

generated quantities {
  real total = sum(alpha[1:p]);
  vector[T - p] log_lik;
  vector[T - p] y_pred;

  for (i in 1:(T - p)) {
    y_pred[i] = student_t_rng(nu, 0, sqrt(sigma2[i]));
    log_lik[i] = student_t_lpdf(y[i + p] | nu, 0, sqrt(sigma2[i]));
  }
}
\end{code}

## GARCH(p, q)

\begin{code}{Stan}{General ARCH(p) model}{code-arch}
data {
  int<lower=1> T; // Length of the TS
  int<lower=1> p; // Order of GARCH(p, q)
  int<lower=1> q; // Order of GARCH(p, q)
  vector[T] y;    // Log of returns
}

parameters {
  real<lower=0> alpha0;     // Intercept
  simplex[p + q + 1] coefs; // All coefs
  real<lower=1> nu;         // Degrees of freedom
}

transformed parameters {
  vector[T - p] sigma2 = rep_vector(alpha0, T - p);  // Conditional variance
  vector[p] alpha = coefs[1:p];                      // Coefficients
  vector[q] beta coefs[(p+1):(p + q)];               // Coefficients

  // Add past residuals
  for (t in 1:(T - p)) {
    for (j in 1:p) {
      sigma2[t] += alpha[j] * square(y[t + p - j]);
    }
  }

  // Add past conditional variance
  for (t in (q + 1):(T - p)) {
    for (i in 1:q) {
      sigma2[t] += beta[i] * sigma2[t - i];
    }
  }
}

model {
  nu     ~ gamma(4, 0.4);
  alpha0 ~ gamma(2, 4);
  alpha  ~ beta(2, 2);
  beta   ~ beta(2, 2);

  for (i in (q + 1):(T-p)) {
    y[i + p] ~ student_t(nu, 0, sqrt(sigma2[i]));
  }
}

generated quantities {
  real total = sum(alpha) + sum(beta);
  vector[T - p] log_lik;
  vector[T - p] y_pred;

  for (i in 1:(T - p)) {
    y_pred[i] = student_t_rng(nu, 0, sqrt(sigma2[i]));
    log_lik[i] = student_t_lpdf(y[i + p] | nu, 0, sqrt(sigma2[i]));
  }
}
\end{code}

## SV

\begin{code}{Stan}{General ARCH(p) model}{code-arch}
data {
  int<lower=1> T;       // Number of time periods
  vector[T] y;          // Observed returns
}

parameters {
  real<lower=0, upper=1> phi_raw; // Persistence of volatility
  real<lower=0> mu;               // Mean of the log volatility
  real<lower=2> nu_la;            // Degrees of freedom for latent volatility
  real<lower=2> nu_in;            // Degrees of freedom for observations
  vector[T] h;                    // Latent log volatilities
}

transformed parameters {
  real<lower=-1,upper=1> phi = 2 * phi_raw - 1;
}

model {
  phi_raw ~ beta(.7, .7);    // Prior for phi, transformed to [-1, 1]
  mu      ~ normal(0, 1);    // Prior for mu
  nu_la   ~ gamma(4.5, 0.2); // Prior for latent df
  nu_in   ~ gamma(4.5, 0.2);  // Prior for observation df

  // Initial state
  h[1] ~ student_t(nu_la, mu, 1 / sqrt(1 - phi * phi));

  // Latent volatility
  for (i in 2:T) {
    h[i] ~ student_t(nu_la, mu + phi * (h[i - 1] - mu), 1);
  }

  // Observation equation
  for (i in 1:T) {
    y[i] ~ student_t(nu_in, 0, exp(h[i] / 2));
  }
}

generated quantities {
  vector[T] y_pred;
  vector[T] log_lik;

  for (i in 1:T) {
    y_pred[i] = student_t_rng(nu_in, 0, exp(h[i] / 2));
    log_lik[i] = student_t_lpdf(y[i] | nu_in, 0, exp(h[i] / 2));
  }
}
\end{code}